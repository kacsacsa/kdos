[org 0x7C00]
[bits 16]

start:
    mov [BOOT_DISK], dl

    cli
    lgdt [GDT_descriptor]
    mov eax, cr0
    or eax, 1
    mov cr0, eax
    jmp CODE_SEG:start_protected_mode  ; far jump -> enters protected mode

; ------------- GDT ----------------
GDT_start:
    GDT_null:
        dq 0x0000000000000000

    GDT_code:
        dw 0xFFFF      ; limit
        dw 0x0000      ; base low
        db 0x00        ; base middle
        db 10011010b   ; access
        db 11001111b   ; granularity
        db 0x00        ; base high

    GDT_data:
        dw 0xFFFF
        dw 0x0000
        db 0x00
        db 10010010b
        db 11001111b
        db 0x00

GDT_end:

GDT_descriptor:
    dw GDT_end - GDT_start - 1
    dd GDT_start

CODE_SEG equ GDT_code - GDT_start
DATA_SEG equ GDT_data - GDT_start

BOOT_DISK: db 0

; ------------- Protected mode ----------------
[bits 32]
start_protected_mode:
    mov ax, DATA_SEG
    mov ds, ax
    mov es, ax
    mov fs, ax
    mov gs, ax
    mov ss, ax
    mov esp, 0x90000

    mov al, 'A'
    mov ah, 0x4F
    mov [0xb8000], ax

    jmp $

; ------------- Boot signature ----------------
times 510 - ($ - $$) db 0
dw 0xAA55
